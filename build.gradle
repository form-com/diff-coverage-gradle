import java.nio.file.Files

buildscript {
    ext.kotlin_version = '1.3.71'

    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.form-com.diff-coverage-gradle:diff-coverage:0.6.0'
    }
}

repositories {
    mavenCentral()
    jcenter()
}

apply plugin: 'jacoco'
apply plugin: 'com.form.diff-coverage'

group 'com.form'

subprojects{
    apply plugin: 'kotlin'
    apply plugin: 'maven'
    apply plugin: 'jacoco'

    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        compile 'org.jacoco:org.jacoco.core:0.8.4'
        compile 'org.jacoco:org.jacoco.report:0.8.4'
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.11.0'

        testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.3.2'
        testImplementation "io.mockk:mockk:1.9.3.kotlin12"
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    sourceCompatibility = 1.8

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    jacoco {
        toolVersion = "0.8.5"
    }

    jacocoTestReport {
        reports {
            html.enabled = true
        }
        executionData.from fileTree(buildDir).include("/jacoco/*.exec")
    }
}

task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
    group 'verification'
    description = 'Generates an aggregate report from all subprojects'
    dependsOn(subprojects.test)

    additionalSourceDirs.from = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.from = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.from = files(subprojects.sourceSets.main.output)
    executionData.from = files(subprojects.jacocoTestReport.executionData)

    reports {
        xml.enabled = true
        html.enabled = true
    }
}

ext.createDiffUrl = { ->
    def diffBase = project.hasProperty('diffBase') ? project.diffBase : 'HEAD'
    afterEvaluate {
        logger.info("Computing coverage for changes between $diffBase and current state (including uncommitted)")
    }
    def file = Files.createTempFile(URLEncoder.encode(project.name, 'UTF-8'), '.diff').toFile()
    file.withOutputStream { out ->
        exec {
            commandLine 'git', 'diff', '--no-color', '--minimal', diffBase
            standardOutput = out
        }
    }
    return file.toURI().toURL()
}

diffCoverageReport {
    afterEvaluate {
        diffSource.url = createDiffUrl()
    }
    jacocoExecFiles = files(subprojects.jacocoTestReport.executionData)
    classesDirs = files(subprojects.jacocoTestReport.classDirectories)
    srcDirs = files(subprojects.jacocoTestReport.sourceDirectories)
    reports {
        html = true
    }

    violationRules {
        minBranches = 0.9
        minLines = 0.9
        minInstructions = 0.9
        failOnViolation = true
    }
}

task clean {
    doLast {
        delete project.buildDir
    }
}